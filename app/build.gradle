apply plugin: 'com.android.application'

//用于获取当前时间
def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'src/main/java-gen']
        }
    }
    signingConfigs {
        release {
            try {
                storeFile file("template.jks")
                storePassword KEYSTORE_PASSWORD
                keyAlias "template"
                keyPassword KEY_PASSWORD
            } catch (ex) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }
    defaultConfig {
        applicationId "com.hx.template"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        // dex突破65535的限制
        multiDexEnabled true
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }
    buildTypes {
        release {
            // zip压缩
//            zipAlignEnabled true
            // 移除无用的resource文件
//            shrinkResources true
            // 混淆代码开启
            minifyEnabled true
            // 混淆配置文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 签名
            signingConfig signingConfigs.release
            // 分渠道打包后文件名（项目名_V版本号_时间_渠道名.apk）
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为 umengtest_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "Template_V${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
        debug {
        }
    }
    //渠道
    productFlavors {
        wandoujia {
        }
        baidu {
        }
        u360 {
        }
        tencent {
        }
        productFlavors.all {
                //设UMENG_CHANNEL_VALUE的值为对应的渠道名(wandoujia、baidu、u360、tencent)
            flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }
    //打包时忽略以下文件
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }
    //解决android 6.0后移除apache http问题
    useLibrary 'org.apache.http.legacy'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'
    //内存泄露检测(debug时候用就好)
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    //Gson(JSON)
    compile 'com.google.code.gson:gson:2.2.4'
    //volley(网络请求)
    compile 'com.mcxiaoke.volley:library:1.0.19'
    //EventBus(事件总线)
    compile 'de.greenrobot:eventbus:2.4.0'
    //Ｂutterknife(依赖注入)
    compile 'com.jakewharton:butterknife:7.0.1'
    //GreenDao(数据库)
    compile 'de.greenrobot:greendao:2.0.0'
    //OrmLite
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    //二维码扫描
    compile 'com.google.zxing:core:3.2.1'
    //广告轮播控件
    compile 'com.bigkoo:convenientbanner:1.0.8'
    //图片裁剪
    compile 'com.edmodo:cropper:1.0.1'
    compile 'com.soundcloud.android:android-crop:1.0.1@aar'
}
